// Made with Meow~ by Риза
// + ---------------------
// | github.com/anominy/movement
// + ---------------------------
// | CONTACTS
// |   - Steam  ::  /id/anominy


// Syntax Information
// + ----------------
// | "<" | ">" w/ something in between - replacement place
// + -----------------------------------------------------
// | <n> | "t"  - <n> tick (ticks) (ex. 1t - 1 tick)
// |               where is "<n>" an integer number of ticks
// | <key>      - replace w/ needed key avoiding the brackets
// |               where is "<key>" the CS:GO key name
// + --------------------------------------------------------
// | "_"         as a prefix     - alternative version of an already defined alias
// |                                that uses any actions w/ 1 tick delay
// |                                or an 1 tick delay action itself
// | <i>         as a suffix     - alternative version of an already defined alias
// |                                where is "<i>" an integer number from 1 to +inf
// | "|"         as a delimiter  - collects expressions into a sequence of themself
// | "::"        as a delimiter  - condition under which current expression will be performed
// | "]::"       as a delimiter  - condition under which all expressions will be performed
// | <i> | "::"  as a delimiter  - condition under which an <i> expression will be performed
// |                                where is "<i>" an integer number from 0 to +inf



// === [ GUARDS ] ===
alias include__std_def



// === [ INCLUDES ] ===
include__internal



// === [ MOVEMENT BLOCK ACTIONS ] ===
alias m_fw "-forward; -back"        // block  forward   keys   ::  until release
alias m_sw "-moveright; -moveleft"  // block  sideways  keys   ::  until release



// === [ MOVEMENT JUMP ACTIONS ] ===

// LongJump on key down w/ forward block
alias +lj_fw "+jump; m_fw"  // execute  +jump  action  |  block  forward   keys  ]::  until release
alias -lj_fw "-jump"        // release  +jump  action

// LongJump on key down w/ sideways block
alias +lj_sw "+jump; m_sw"  // execute  +jump  action  |  block  sideways  keys  ]::  until release
alias -lj_sw "-jump"        // release  +jump  action

// --------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------

// CrouchJump on key down
alias +cj "+jump; +duck"  // execute  +jump +duck  actions  ::  until release
alias -cj "-jump; -duck"  // release  +jump +duck  actions

// CrouchJump on key down w/ forward block
alias +cj_fw "+cj; m_fw"  // execute  +cj  action  |  block  forward   keys  ]::  until release
alias -cj_fw "-cj"        // release  +cj  action

// CrouchJump on key down w/ sideways block
alias +cj_sw "+cj; m_sw"  // execute  +cj  action  |  block  sideways  keys  ]::  until release
alias -cj_sw "-cj"        // release  +cj  action

// --------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------

// MiniJump on key down
alias +mj "+jump; _duck"    // execute  +jump _duck  actions  0::  until release
alias -mj "-jump"           // release  +jump        action

// MiniJump on key down w/ forward block
alias +mj_fw "+mj; m_fw"    // execute  +mj          action  |  block    forward   keys    ]::  until release
alias -mj_fw "-mj"          // release  +mj          action

// MiniJump on key down w/ sideways block
alias +mj_sw "+mj; m_sw"    // execute  +mj          action  |  block    sideways  keys    ]::  until release
alias -mj_sw "-mj"          // release  +mj          action

// --------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------

// === [ MOVEMENT ACTIONS W/ JUMP/DUCK RELEASE ] ===

// LongJump on key down w/ forward block
alias _lj_fw "_jump; m_fw"  // execute  _jump  action  |  block  forward   keys   ::  until release

// LongJump on key down w/ sideways block
alias _lj_sw "_jump; m_sw"  // execute  _jump  action  |  block  sideways  keys   ::  until release

// --------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------

// CrouchJump on key down
alias +_cj "_jump; +duck"  // execute  _jump +duck  actions                     1::  until release
alias -_cj "-duck"         // release  +duck        action

// CrouchJump on key down w/ forward block
alias +_cj_fw "+_cj; m_fw"  // execute  +_cj  action  |  block  forward   keys  ]::  until release
alias -_cj_fw "-_cj"        // release  +_cj  action

// CrouchJump on key down w/ sideways block
alias +_cj_sw "+_cj; m_sw"  // execute  +_cj  action  |  block  sideways  keys  ]::  until release
alias -_cj_sw "-_cj"        // release  +_cj  action

// --------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------

// MiniJump on key down
alias _mj "_jump; _duck"  // execute  _jump _duck  actions

// MiniJump on key down w/ forward block
alias _mj_fw "_mj; m_fw"  // execute  _mj          action   |  block  forward   keys   ::  until release

// MiniJump on key down w/ sideways block
alias _mj_sw "_mj; m_sw"  // execute  _mj          action   |  block  sideways  keys   ::  until release

// --------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------

// CrouchBug on key down
// +--------------------
// | WARNING: This is just an alias over the "_duck" action to maintain the same name standards for layouts
// |  prefer to use the first-class actions for bindings // consistent & clarify are the keys to success
alias _cb "_duck"  // execute  _duck  action

// --------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------

// LongJump on key down w/ forward block
// +------------------------------------
// | WARNING: This is just an alias over the "_lj_fw" action to maintain the same name standards for layouts
// |  prefer to use the first-class actions for bindings // consistent & clarify are the keys to success
alias +_lj_fw "_lj_fw"  // execute  _lj_fw  action
alias -_lj_fw

// LongJump on key down w/ sideways block
// +-------------------------------------
// | WARNING: This is just an alias over the "_lj_sw" action to maintain the same name standards for layouts
// |  prefer to use the first-class actions for bindings // consistent & clarify are the keys to success
alias +_lj_sw "_lj_sw"  // execute  _lj_sw  action
alias -_lj_sw

// --------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------

// MiniJump on key down
// +-------------------
// | WARNING: This is just an alias over the "_mj" action to maintain the same name standards for layouts
// |  prefer to use the first-class actions for bindings // consistent & clarify are the keys to success
alias +_mj "_mj"        // execute  _mj     action
alias -_mj

// MiniJump on key down w/ forward block
// +------------------------------------
// | WARNING: This is just an alias over the "_mj_fw" action to maintain the same name standards for layouts
// |  prefer to use the first-class actions for bindings // consistent & clarify are the keys to success
alias +_mj_fw "_mj_fw"  // execute  _mj_fw  action
alias -_mj_fw

// MiniJump on key down w/ sideways block
// +-------------------------------------
// | WARNING: This is just an alias over the "_mj_sw" action to maintain the same name standards for layouts
// |  prefer to use the first-class actions for bindings // consistent & clarify are the keys to success
alias +_mj_sw "_mj_sw"  // execute  _mj_sw  action
alias -_mj_sw

// --------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------

// CrouchBug on key down
// +--------------------
// | WARNING: This is just an alias over the "_duck" action to maintain the same name standards for layouts
// |  prefer to use the first-class actions for bindings // consistent & clarify are the keys to success
alias +cb "_cb"   // execute  _cb  action
alias -cb

// CrouchBug on key release
alias +cb1
alias -cb1 "_cb"  // execute  _cb  action



// === [ MOVEMENT STRAFE ACTIONS ] ===

// Strafe storage
alias backup_forward    // backup  +forward    action
alias backup_back       // backup  +back       action
alias backup_moveright  // backup  +moveright  action
alias backup_moveleft   // backup  +moveleft   action


// Nulled +forward w/ backup
alias +null_forward "+forward; -back; alias backup_forward +forward"              // execute  +forward    action                                   |  release  +back       action  |  store  +forward    in  backup_forward    storage  0::  until release or opposite action
alias -null_forward "backup_back; -forward; alias backup_forward"                 // execute  +back       action  from  backup_back       storage  |  release  +forward    action  |  clear  backup_forward    storage

// Nulled +back w/ backup
alias +null_back "+back; -forward; alias backup_back +back"                       // execute  +back       action                                   |  release  +forward    action  |  store  +back       in  backup_back       storage  0::  until release or opposite action
alias -null_back "backup_forward; -back; alias backup_back"                       // execute  +forward    action  from  backup_forward    storage  |  release  +back       action  |  clear  backup_back       storage

// Nulled +moveright w/ backup
alias +null_moveright "+moveright; -moveleft; alias backup_moveright +moveright"  // execute  +moveright  action                                   |  release  +moveleft   action  |  store  +moveright  in  backup_moveright  storage  0::  until release or opposite action
alias -null_moveright "backup_moveleft; -moveright; alias backup_moveright"       // execute  +moveleft   action  from  backup_moveleft   storage  |  release  +moveright  action  |  clear  backup_moveright  storage

// Nulled +moveleft w/ backup
alias +null_moveleft "+moveleft; -moveright; alias backup_moveleft +moveleft"     // execute  +moveleft   action                                   |  release  +moveright  action  |  store  +moveleft   in  backup_moveleft   storage  0::  until release or opposite action
alias -null_moveleft "backup_moveright; -moveleft; alias backup_moveleft"         // execute  +moveright  action  from  backup_moveright  storage  |  release  +moveleft   action  |  clear  backup_moveleft   storage

// --------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------

// Nulled +forward
alias +null1_forward "+forward; -back"          // execute  +forward    action  |  release  +back       action  0::  until release
alias -null1_forward "-forward"                 // release  +forward    action

// Nulled +back
alias +null1_back "+back; -forward"             // execute  +back       action  |  release  +forward    action  0::  until release
alias -null1_back "-back"                       // release  +back       action

// Nulled +moveright
alias +null1_moveright "+moveright; -moveleft"  // execute  +moveright  action  |  release  +moveleft   action  0::  until release
alias -null1_moveright "-moveright"             // release  +moveright  action

// Nulled +moveleft
alias +null1_moveleft "+moveleft; -moveright"   // execute  +moveleft   action  |  release  +moveright  action  0::  until release
alias -null1_moveleft "-moveleft"               // release  +moveleft   action
